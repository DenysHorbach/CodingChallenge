plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.openapi.generator' version '6.6.0'
}

group = 'com.dhorbach'
version = '1.0.0'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.1.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'org.modelmapper:modelmapper:3.1.1'
	implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    compileOnly 'org.projectlombok:lombok:1.18.28'
	annotationProcessor 'org.projectlombok:lombok:1.18.28'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test:3.5.7'
	testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'
	testImplementation 'org.projectlombok:lombok:1.18.28'

}

tasks.named('test') {
	useJUnitPlatform()
}

def generatedCodeGroup = "$buildDir/generated/openapi"

openApiGenerate {
	generatorName.set("spring")
	inputSpec.set("$rootDir/openapi/openapi.yaml")
	outputDir.set("$generatedCodeGroup")
	apiPackage.set("api")
	modelPackage.set("models")
	configOptions.set([
			interfaceOnly: "true",
			skipDefaultInterface : "true",
			useSpringBoot3: "true",
			useTags: "true",
			reactive: "true",
			serializableModel: "true"
	])
}

sourceSets.main.java.srcDirs += "$generatedCodeGroup/src/main/java"

//removes generation of separate *-plain-*.jar
jar {
	enabled = false
}